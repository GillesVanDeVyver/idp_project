// First name: <your first name>
// Last name: <your last name>
// Student number: <your student number>


// FIXED VOCABULARY
// DO NOT CHANGE THIS VOCABULARY
LTCvocabulary V_fixed {
    type Time isa nat			// A set of time points
    Start: Time 				// The initial time point
    partial Next(Time): Time 	// The successor time point of time point t
    
    type Floor isa nat									// The set of floors
    type DoorState constructed from {Closed, Open}		// A set of possible states of the elevator’s doors
	
    // Fluents
    ElPosition(Time):Floor				// The floor of the elevator at time point t
    ElDoorState(Time):DoorState			// The state of the elevator’s doors at time point t
    UnansweredRequest(Time, Floor)		// Denotes that there is an unanswered request at floor f at time point t
    
    // Actions
    MakeRequest(Time, Floor) 			// Denotes that a request is made for floor f at time point t
}


// STUDENT VOCABULARY
// Here you can add new types, functions and predicates
LTCvocabulary V_student {
    extern vocabulary V_fixed
    I_floor : Floor
    I_DoorState : DoorState
    C_floor(Time, Floor)
    //Cn_floor(Time)
    C_Unanswered(Time,Floor)
    Cn_Unanswered(Time,Floor)
	GoingUp(Time)
 	GoingDown(Time)
    requestAbove(Time) 
    requestBelow(Time)
    C_DoorOpen(Time)
    FreshRequestsHelper(Time)


}

// TIME THEORY
// DO NOT CHANGE THIS THEORY
Theory T_fixed : V_student {
    // Defining time
    {
        Start = MIN[:Time].
        ! t: Next(t) = t+1 <- Time(t+1).
    }
}


// STUDENT THEORY
// Here you should model the dynamic elevator system
Theory T_student:V_student {
    I_floor = 0.
    I_DoorState = Closed.
    {
        ElDoorState(Start)= I_DoorState.
        ! t[Time]: ElDoorState(Next(t))= Open <- C_DoorOpen(t).
        ! t[Time]: ElDoorState(Next(t))= Closed <- ElDoorState(t)=Open.
        ! t[Time]: ElDoorState(Next(t))= Closed <- ElDoorState(t)=Closed & ~C_DoorOpen(t).

    }

    {
        ElPosition(Start)= I_floor.
        ! t[Time] f[Floor]: ElPosition(Next(t))= f <- C_floor(t, f).
        ! t[Time] f[Floor]: ElPosition(Next(t))= f <- ElPosition(t)= f &
        	~(?f2[Floor]: C_floor(t,f2) & f ~= f2).
    }
    {
        !t[Time] f[Floor]: C_Unanswered(t,f) <- MakeRequest(t,f) & ~UnansweredRequest(t,f).
    }
    {
    	! t[Time] f[Floor]: UnansweredRequest(Next(t),f) <- C_Unanswered(t, f).
        ! t[Time] f[Floor]: UnansweredRequest(Next(t),f) <- UnansweredRequest(t,f)& ~Cn_Unanswered(t, f).
    }
    {
        ! t[Time] f[Floor]: Cn_Unanswered(t,f) <- ElPosition(t)=f & ElDoorState(t)=Open.
    }
    {
        ! t[Time] f[Floor]: C_floor(t, f+1) <- GoingUp(t) & ElPosition(t)=f &
        ~UnansweredRequest(t,f). // take into account max floor?
        ! t[Time] f[Floor]: C_floor(t, f-1) <- GoingDown(t) & ElPosition(t)=f &
        ~UnansweredRequest(t,f). // take into account max floor?
    }
    {
        ! t[Time] f[Floor]: C_DoorOpen(t) <- ElPosition(t)=f &
        UnansweredRequest(t,f) & ElDoorState(t) = Closed.        
    }    
    {
         ! t[Time] f1[Floor]: requestAbove(t) <- ElPosition(t)=f1&
			?f2[Floor]:UnansweredRequest(t,f2)&f2>f1. //| MakeRequest(t,f2)&f2>f1.
        ! t[Time] f1[Floor]: requestBelow(t) <- ElPosition(t)=f1&
			?f2[Floor]:UnansweredRequest(t,f2)&f2<f1. //| MakeRequest(t,f2)&f2<f1.
    }
    ! t[Time]: FreshRequestsHelper(t) <=> requestAbove(t) & requestBelow(t) & ~GoingUp(t-1) &
    	~GoingDown(t-1). // this helper predicates indicates that there are new unhandled requests 		and the elvator was not going up or down. So we need to decide what request to handle first
    {
      // 'obvious' cases
      ! t[Time]:  GoingUp(t) <- requestAbove(t) & ~requestBelow(t).
      ! t[Time]:  GoingDown(t) <- requestBelow(t) & ~requestAbove(t).
      // keep going the direction the elevator was going
      ! t[Time]:  GoingUp(t) <- requestAbove(t) & requestBelow(t) & GoingUp(t-1).
	  ! t[Time]:  GoingDown(t) <- requestAbove(t) & requestBelow(t) & GoingDown(t-1).
      // handling of 'fresh requests'
      ! t[Time], f1[Floor]:  GoingUp(t) <- FreshRequestsHelper(t) & ElPosition(t)=f1 & 
        min{f2[Floor]: UnansweredRequest(t,f2)&f2>f1: abs(f2-f1)}<min{f2[Floor]:
        UnansweredRequest(t,f2)&f2<f1 : abs(f2-f1)}.
      ! t[Time], f1[Floor]:  GoingDown(t) <- FreshRequestsHelper(t) & ElPosition(t)=f1 
        & min{f2[Floor]: UnansweredRequest(t,f2)&f2>f1: abs(f2-f1)}>min{f2[Floor]:
        UnansweredRequest(t,f2)&f2<f1 : abs(f2-f1)}.
      ! t[Time], f1[Floor]:  GoingUp(t) <- FreshRequestsHelper(t) & ElPosition(t)=f1 & 
        min{f2[Floor]: UnansweredRequest(t,f2)&f2>f1: abs(f2-f1)}=min{f2[Floor]:
        UnansweredRequest(t,f2)&f2<f1 : abs(f2-f1)}.        
    }
    ~(?f[Floor]: UnansweredRequest(Start,f)). // we start without  active unanswered requests
}



// MAIN PROCEDURE
// If you have to change the main procedure for debugging purposes, be careful and restore it back once you finish debugging.
procedure main() {
    local CompleteTheory = merge(T_fixed, T_student) 
    
    // Change the structure name to try other structures (check the included file "structures.idp" for available provided structures, and feel free to add additional structures)                           
    //explainunsat(CompleteTheory,S5)
    //printmodels(allmodels(CompleteTheory,S10))
    model = onemodel(CompleteTheory, S10)
    
    // Printing model 
    print(model)

    // Visualization
    initVisualization()
    visualizeElevator(model)
}

// File responsible for visualization  
include "visualize_elevator.idp"

// Structures for testing
// You can open this file and check the given structures 
include "structures.idp"
